plugins {
	id 'fabric-loom' version "${loom_version}"
}

dependencies {
	implementation project(':base')
	minecraft group: 'com.mojang', name: 'minecraft', version: project.minecraft_version
	mappings loom.officialMojangMappings()
	modImplementation group: 'net.fabricmc', name: 'fabric-loader', version: project.loader_version

	modImplementation fabricApi.module('fabric-command-api-v1', project.fabric_api)
}

loom {
	mixin {
		defaultRefmapName = 'ChatComponentAPI-fabric-refmap.json'
	}
}

processResources {
	var replaceProperties = [
		version: project.version,
		license: project.license,
	]
	inputs.properties replaceProperties

	filesMatching('fabric.mod.json') {
		expand replaceProperties
	}
}

jar {
	var task = project(':base').tasks.getByName('jar')

	from(zipTree(task.archiveFile)) {
		exclude 'LICENSE'
	}

	manifest {
		from task.manifest
	}
}

task remapDevJar(type: net.fabricmc.loom.task.RemapJarTask) {
	var task = project.tasks.getByName('devJar')
	archiveBaseName = "Dev${project.base.archivesName.get()}"
	input.set(task.archiveFile)
}

devJar.finalizedBy remapDevJar
